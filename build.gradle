group 'net.bndy'
version '1.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    maven { url "https://oss.sonatype.org/content/groups/staging" }
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.lucene', name: 'lucene-core', version: '7.2.1'
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version: '7.2.1'
    compile group: 'net.bndy', name: 'lib', version: '1.0.8'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task copyDocs(type: Copy, dependsOn: javadocJar) {
    from 'build/docs'
    into 'docs'
}
javadocJar.finalizedBy(copyDocs)

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Full Text Search Interface'
                packaging 'jar'

                // optionally artifactId can be defined here
                description 'Full Text Search Interface based on Lucene'
                url 'https://github.com/bndynet/ftsi'

                organization {
                    name 'BNDY-NET'
                    url 'http://bndy.net'
                }

                scm {
                    url 'https://github.com/bndynet/ftsi'
                    connection 'scm:git:git://github.com/bndynet/ftsi.git'
                    developerConnection 'scm:git:git@github.com:bndynet/ftsi.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'bndynet'
                        name 'Bendy Zhang'
                        email 'zb@bndy.net'
                        url 'http://bndy.net'
                    }
                }
            }
        }
    }
}